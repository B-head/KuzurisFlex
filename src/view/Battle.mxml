<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
               xmlns:s="library://ns.adobe.com/flex/spark" 
               xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:v="view.*"
			   initialize="init()"
			   keyDown="onKeyDown(event)"
			   enterFrame="onEnterFrame()">
			   
	<fx:Script>
		<![CDATA[
		import event.*;
		import flash.events.*;
		import flash.ui.*;
		import model.*;
		import model.ai.*;
		
		private var manager:GameManager;
		[Bindable]
		private var input1:GameControl;
		[Bindable]
		private var input2:GameControl;
		[Bindable]
		private var record1:GameRecord;
		[Bindable]
		private var record2:GameRecord;
		[Bindable]
		private var pause:Boolean;
		[Bindable]
		private var replayable:Boolean;
		private var autoStart:Boolean;
		
		public function init():void
		{
			parentApplication.addEventListener(Main.pauseEvent, pauseListener);
			manager = new GameManager(2);
			manager.addEventListener(GameManagerEvent.gameEnd, endListener);
			input1 = SharedObjectHelper.input;
			input2 = GameAIManager.createDefaultAIManager();
			manager.initialize();
			player1.gameModel = manager.getGameModel(0);
			player2.gameModel = manager.getGameModel(1);
			player1.blockGraphics = new BlockGraphics();
			player2.blockGraphics = new BlockGraphics();
			prepare();
		}
		
		private function onKeyDown(e:KeyboardEvent):void
		{
			if (e.shiftKey && e.ctrlKey && e.keyCode == Keyboard.A)
			{
				autoStart = !autoStart;
			}
		}
		
		private function changePlayer(p2:Boolean):void
		{
			if (input1 is UserInput && input2 is UserInput)
			{
				if (p2)
				{
					input1 = SharedObjectHelper.input;
					input2 = GameAIManager.createDefaultAIManager();
				}
				else
				{
					input1 = GameAIManager.createDefaultAIManager();
					input2 = SharedObjectHelper.input;
				}
			}
			else if (p2)
			{
				if (input1 is UserInput)
				{
					input1 = SharedObjectHelper.inputVersus1;
					input2 = SharedObjectHelper.inputVersus2;
				}
				else if (input2 is UserInput)
				{
					input2 = GameAIManager.createDefaultAIManager();
				}
				else
				{
					input2 = SharedObjectHelper.input;
				}
			}
			else
			{
				if (input2 is UserInput)
				{
					input1 = SharedObjectHelper.inputVersus1;
					input2 = SharedObjectHelper.inputVersus2;
				}
				else if (input1 is UserInput)
				{
					input1 = GameAIManager.createDefaultAIManager();
				}
				else
				{
					input1 = SharedObjectHelper.input;
				}
			}
		}
		
		private function prepare():void
		{
			parentApplication.bgm.stop();
			manager.endGame();
			currentState = "prepare";
			pause = false;
		}
		
		private function start():void
		{
			var setting:GameSetting = new GameSetting();
			manager.initialize();
			manager.setPlayer(0, input1);
			manager.setPlayer(1, input2);
			manager.setAILevel(0, aiLevel1.value);
			manager.setAILevel(1, aiLevel2.value);
			player1.gameModel = manager.getGameModel(0);
			player2.gameModel = manager.getGameModel(1);
			manager.startGame(setting);
			parentApplication.bgm.play(false);
			currentState = "hidden";
			setFocus();
			pause = false;
			replayable = false;
		}
		
		private function replayStart():void
		{
			manager.initialize();
			player1.gameModel = manager.getGameModel(0);
			player2.gameModel = manager.getGameModel(1);
			manager.startReplay();
			parentApplication.bgm.play(false);
			currentState = "hidden";
			setFocus();
			pause = false;
		}
		
		private function pauseing():void
		{
			parentApplication.bgm.stop();
			manager.phaseGame();
			currentState = "pause";
			pause = true;
		}
		
		private function resume():void
		{
			parentApplication.bgm.play(true);
			manager.resumeGame();
			currentState = "hidden";
			setFocus();
			pause = false;
		}
		
		private function onEnterFrame():void
		{
			if (autoStart && currentState == "prepare")
			{
				start();
			}
			if (!manager.isExecution()) return;
			manager.forwardGame();
		}
		
		private function pauseListener(e:Event):void
		{
			if (pause)
			{
				resume();
			}
			else
			{
				if (!manager.isExecution()) return;
				pauseing();
			}
		}
		
		private function endListener(e:GameManagerEvent):void
		{
			replayable = true;
			record1 = manager.getRecord(0);
			record2 = manager.getRecord(1);
			prepare();
		}
		]]>
	</fx:Script>

	<fx:Metadata>
		[Event(name="back", type="flash.events.Event")]
	</fx:Metadata>
	
	<s:states>
		<s:State name="hidden" />
		<s:State name="prepare" />
		<s:State name="pause" />
		<s:State name="keyConfig" />
	</s:states>
	
	<v:GameView id="player1" x="18" y="24" battle="true" reverse="false" pause="{pause}"/>
	<v:GameView id="player2" x="436" y="24" battle="true" reverse="true" pause="{pause}"/>
	<s:VGroup includeIn="prepare" x="143" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button styleName="start" width="150" label="スタート" buttonDown="start()" />
		<s:Button width="150" label="リプレイ" buttonDown="replayStart()" enabled="{replayable}" />
		<s:Button width="150" label="キーコンフィグ" enabled="{input1 is UserInput || input2 is UserInput}" buttonDown="currentState = 'keyConfig'"/>
		<s:Button styleName="back" width="100" label="もどる" buttonDown="dispatchEvent(new Event('back'))" />
	</s:VGroup>
	<s:VGroup includeIn="prepare" x="143" y="340" width="150" horizontalAlign="center">
		<s:ButtonBar id="playerType1" width="150" requireSelection="true" selectedIndex="0" changing="changePlayer(false)">  
			<mx:ArrayCollection source="['人間', 'AI']"/>
		</s:ButtonBar>
		<s:Label styleName="menu-label" text="AIレベル{aiLevel1.value}" visible="{playerType1.selectedIndex == 1}" />
		<s:HSlider id="aiLevel1" width="150" minimum="1" maximum="20" showDataTip="false" visible="{playerType1.selectedIndex == 1}" />
	</s:VGroup>
	<s:VGroup includeIn="prepare" x="561" y="340" width="150" horizontalAlign="center">
		<s:ButtonBar id="playerType2" width="150" requireSelection="true" selectedIndex="1" changing="changePlayer(true)">  
			<mx:ArrayCollection source="['人間', 'AI']"/>
		</s:ButtonBar>
		<s:Label styleName="menu-label" text="AIレベル{aiLevel2.value}" visible="{playerType2.selectedIndex == 1}" />
		<s:HSlider id="aiLevel2" width="150" minimum="1" maximum="20" showDataTip="false" visible="{playerType2.selectedIndex == 1}" />
	</s:VGroup>
	<s:VGroup includeIn="pause" x="143" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button width="100" label="つづける" buttonDown="resume()" />
		<s:Button width="100" label="やりなおす" buttonDown="start()" />
		<s:Button styleName="back" width="100" label="おわる" buttonDown="prepare()" />
	</s:VGroup>
	<v:DetailedRecordView includeIn="prepare" x="0" y="0" width="118" height="480" visible="{replayable}" battle="true" record="{record1}" />
	<v:DetailedRecordView includeIn="prepare" x="736" y="0" width="118" height="480" visible="{replayable}" battle="true" record="{record2}" />
	<v:KeyConfig includeIn="keyConfig" x="18" y="88" input="{input1 as UserInput}" visible="{input1 is UserInput}" back="currentState = 'prepare'"/>
	<v:KeyConfig includeIn="keyConfig" x="436" y="88" input="{input2 as UserInput}" visible="{input2 is UserInput}" back="currentState = 'prepare'"/>
</s:Group>
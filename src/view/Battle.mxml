<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx"
		   xmlns:v="view.*"
		   initialize="init()"
		   frameConstructed ="manager.frameConstructedListener()">
			   
	<fx:Script>
		<![CDATA[
		import events.*;
		import flash.events.*;
		import flash.ui.*;
		import model.*;
		import model.ai.*;
		
		[Bindable]
		private var manager:GameManager;
		[Bindable]
		private var input1:GameControl;
		[Bindable]
		private var input2:GameControl;
		[Bindable]
		private var isResult:Boolean;
		private var autoStart:Boolean;
		
		private function init():void
		{
			parentApplication.addEventListener(KuzurisEvent.pressPauseKey, pauseListener);
			stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown);
			manager = new GameManager(2);
			manager.addEventListener(KuzurisEvent.gameStart, startListener);
			manager.addEventListener(KuzurisEvent.gameEnd, endListener);
			input1 = SharedObjectHelper.input;
			input2 = GameAIManager.createDefaultAI();
			manager.initialize();
			prepare();
		}
		
		private function onKeyDown(e:KeyboardEvent):void
		{
			if (e.shiftKey && e.ctrlKey && e.keyCode == Keyboard.A)
			{
				autoStart = !autoStart;
				if (autoStart && currentState == "prepare")
				{
					start();
				}
			}
		}
		
		private function changePlayer():void
		{
			if (playerType1.selectedIndex == 0 && playerType2.selectedIndex == 0)
			{
				input1 = SharedObjectHelper.inputVersus1;
				input2 = SharedObjectHelper.inputVersus2;
			}
			else
			{
				input1 = getPlayer(playerType1.selectedIndex);
				input2 = getPlayer(playerType2.selectedIndex);
			}
		}
		
		private function getPlayer(si:int):GameControl
		{
			if (si == 0)
			{
				return SharedObjectHelper.input;
			}
			else
			{
				return GameAIManager.createDefaultAI();
			}
		}
		
		private function prepare():void
		{
			parentApplication.bgm.stop();
			manager.endGame();
			currentState = "prepare";
			if (autoStart)
			{
				start();
			}
		}
		
		private function start():void
		{
			manager.initialize();
			manager.setPlayer(0, input1);
			manager.setPlayer(1, input2);
			manager.setAILevel(0, aiLevel1.value);
			manager.setAILevel(1, aiLevel2.value);
			manager.startGame();
			currentState = "hidden";
			setFocus();
			isResult = false;
		}
		
		private function replayStart():void
		{
			manager.initialize();
			manager.startReplay();
			currentState = "hidden";
			setFocus();
			isResult = false;
		}
		
		private function pauseing():void
		{
			if (!manager.isExecution()) return;
			parentApplication.bgm.stop();
			manager.phaseGame();
			currentState = "pause";
		}
		
		private function resume():void
		{
			parentApplication.bgm.play(true);
			manager.resumeGame();
			currentState = "hidden";
			setFocus();
		}
		
		private function pauseListener(e:KuzurisEvent):void
		{
			if (currentState == "pause")
			{
				resume();
			}
			else
			{
				pauseing();
			}
		}
		
		private function startListener(e:KuzurisEvent):void
		{
			parentApplication.bgm.play(false);
		}
		
		private function endListener(e:KuzurisEvent):void
		{
			recordView1.record = manager.getRecord(0);
			recordView2.record = manager.getRecord(1);
			isResult = true;
			prepare();
		}
		]]>
	</fx:Script>

	<fx:Metadata>
		[Event(name="navigateBack", type="events.KuzurisEvent")]
	</fx:Metadata>
	
	<s:states>
		<s:State name="hidden" />
		<s:State name="prepare" />
		<s:State name="pause" />
		<s:State name="keyConfig" />
	</s:states>
	
	<v:GameView id="player1" x="18" y="24" battle="true" reverse="false" playerIndex="0" gameManager="{manager}"/>
	<v:GameView id="player2" x="436" y="24" battle="true" reverse="true" playerIndex="1" gameManager="{manager}" />
	<v:DetailedRecordView id="recordView1" includeIn="prepare" x="0" y="0" width="118" height="480" battle="true" visible="{isResult}" />
	<v:DetailedRecordView id="recordView2" includeIn="prepare" x="736" y="0" width="118" height="480" battle="true" visible="{isResult}" />
	<v:KeyConfig includeIn="keyConfig" x="18" y="88" input="{input1 as UserInput}" visible="{input1 is UserInput}" navigateBack="currentState = 'prepare'"/>
	<v:KeyConfig includeIn="keyConfig" x="436" y="88" input="{input2 as UserInput}" visible="{input2 is UserInput}" navigateBack="currentState = 'prepare'"/>
	<s:VGroup includeIn="prepare" x="143" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button styleName="start" width="150" label="スタート" buttonDown="start()" />
		<s:Button width="150" label="対戦履歴" enabled="false" />
		<s:Button width="150" label="キーコンフィグ" enabled="{input1 is UserInput || input2 is UserInput}" buttonDown="currentState = 'keyConfig'"/>
		<s:Button styleName="back" width="100" label="もどる" buttonDown="dispatchEvent(new KuzurisEvent(KuzurisEvent.navigateBack))" />
	</s:VGroup>
	<s:VGroup includeIn="prepare" x="143" y="340" width="150" horizontalAlign="center">
		<s:ButtonBar id="playerType1" width="150" requireSelection="true" selectedIndex="0" changing="changePlayer()">  
			<mx:ArrayCollection source="['人間', 'AI']"/>
		</s:ButtonBar>
		<s:Label styleName="menu-label" text="AIレベル{aiLevel1.value}" visible="{playerType1.selectedIndex == 1}" />
		<s:HSlider id="aiLevel1" width="150" minimum="1" maximum="20" showDataTip="false" visible="{playerType1.selectedIndex == 1}" />
	</s:VGroup>
	<s:VGroup includeIn="prepare" x="561" y="340" width="150" horizontalAlign="center">
		<s:ButtonBar id="playerType2" width="150" requireSelection="true" selectedIndex="1" changing="changePlayer()">  
			<mx:ArrayCollection source="['人間', 'AI']"/>
		</s:ButtonBar>
		<s:Label styleName="menu-label" text="AIレベル{aiLevel2.value}" visible="{playerType2.selectedIndex == 1}" />
		<s:HSlider id="aiLevel2" width="150" minimum="1" maximum="20" showDataTip="false" visible="{playerType2.selectedIndex == 1}" />
	</s:VGroup>
	<s:VGroup includeIn="pause" x="143" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button width="100" label="つづける" buttonDown="resume()" />
		<s:Button width="100" label="はじめから" buttonDown="start()" />
		<s:Button styleName="back" width="100" label="おわる" buttonDown="prepare()" />
	</s:VGroup>
</s:Group>
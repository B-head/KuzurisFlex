<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
               xmlns:s="library://ns.adobe.com/flex/spark" 
               xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:v="view.*"
			   initialize="init()"
			   frameConstructed ="frameConstructedListener()">
	<fx:Script>
		<![CDATA[
			import events.*;
			import flash.events.*;
			import flash.utils.*;
			import model.*;
			import mx.events.*;
			
			[Bindable]
			public var roomManager:RoomManager;
			[Bindable]
			private var gameManager:GameManager;
			[Bindable]
			private var practiceGameManager:GameManager;
			[Bindable]
			private var isResult:Boolean;
			[Bindable]
			private var isFlip:Boolean;
			
			private function init():void
			{
				parentApplication.addEventListener(KuzurisEvent.pressPauseKey, pauseListener);
				roomManager.addEventListener(NetworkGameReadyEvent.networkGameReady, networkGameReadyListener);
				roomManager.addEventListener(KuzurisEvent.playerUpdate, playerUpdateListener);
				gameManager = roomManager.createGameManager();
				gameManager.addEventListener(KuzurisEvent.gameStart, startListener);
				gameManager.addEventListener(KuzurisEvent.gameEnd, endListener);
				gameManager.initialize();
				practiceGameManager = new GameManager(1);
				practiceGameManager.addEventListener(KuzurisEvent.gameStart, startListener);
				practiceGameManager.addEventListener(KuzurisEvent.gameEnd, practiceEndListener);
				prepare();
			}
			
			private function enterBattle(index:int):void
			{
				roomManager.selfEnterBattle(index);
				isFlip = false;
			}
			
			private function leaveBattle():void
			{
				roomManager.selfLeaveBattle();
			}
		
			private function prepare():void
			{
				if (currentState == "practice")
				{
					if (gameManager.isExecution() || roomManager.isStand()) return;
					parentApplication.bgm.stop();
					practiceGameManager.endGame();
				}
				if (roomManager.isEnter())
				{
					if (gameManager.isExecution() || roomManager.isStand())
					{
						currentState = "stand";
					}
					else
					{
						currentState = "enter";
					}
				}
				else
				{
					currentState = "watch";
				}
			}
			
			private function start():void
			{
				roomManager.gameSync();
			}
			
			private function practiceStart():void
			{
				currentState = "practice";
				practiceGameManager.initialize();
				practiceGameManager.setPlayer(0, roomManager.selfInput);
				practiceGameManager.startGame();
				setFocus();
				isResult = false;
			}
			
			private function networkGameReadyListener(e:NetworkGameReadyEvent):void
			{
				currentState = "hidden";
				gameManager.initialize();
				gameManager.startGame(e.setting, e.seed, e.delay);
				setFocus();
				isResult = false;
			}
		
			private function startListener(e:KuzurisEvent):void
			{
				parentApplication.bgm.play(false);
			}
		
			private function endListener(e:KuzurisEvent):void
			{
				parentApplication.bgm.stop();
				gameManager.endGame();
				practiceGameManager.endGame();
				recordView1.record = gameManager.getRecord(0);
				recordView2.record = gameManager.getRecord(1);
				isResult = true;
				prepare();
			}
			
			private function practiceEndListener(e:KuzurisEvent):void
			{
				parentApplication.bgm.stop();
				practiceGameManager.endGame();
				currentState = "hidden";
				prepare();
			}
		
			private function pauseListener(e:Event):void
			{
				parentApplication.bgm.stop();
				practiceGameManager.endGame();
				currentState = "hidden";
				prepare();
			}
			
			public function playerUpdateListener(e:KuzurisEvent):void
			{
				prepare();
			}
			
			public function frameConstructedListener():void
			{
				gameManager.frameConstructedListener();
				practiceGameManager.frameConstructedListener();
			}
		
		]]>
	</fx:Script>
	
	<fx:Metadata>
		[Event(name="navigateBack", type="events.KuzurisEvent")]
	</fx:Metadata>
	
	<s:states>
		<s:State name="hidden" />
		<s:State name="practice"/>
		<s:State name="watch"/>
		<s:State name="stand" stateGroups="prepare" />
		<s:State name="enter" stateGroups="prepare" />
	</s:states>
	
	<v:GameView id="player1" x="18" y="24" battle="true" reverse="false" playerIndex="{isFlip ? 1 : 0}" playerInfo="{roomManager.getPlayerInfo(isFlip ? 1 : 0)}"
		gameManager="{gameManager}" visible.practice="{roomManager.transFlip(isFlip)}" />
	<v:GameView id="player2" x="436" y="24" battle="true" reverse="true" playerIndex="{isFlip ? 0 : 1}" playerInfo="{roomManager.getPlayerInfo(isFlip ? 0 : 1)}"
		gameManager="{gameManager}" visible.practice="{!roomManager.transFlip(isFlip)}"/>
	<v:GameView id="practicePlayer" x="{roomManager.transFlip(isFlip) ? 436 : 18}" y="24" battle="true" visible="false" visible.practice="true"
		reverse="{roomManager.transFlip(isFlip)}" playerIndex="0" gameManager="{practiceGameManager}"/>
	<s:VGroup includeIn="prepare" x="{roomManager.transFlip(isFlip) ? 561 : 143}" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button includeIn="enter" styleName="start" width="150" label="スタート" buttonDown="start()" />
		<s:Button includeIn="stand" styleName="start" width="150" label="練習" buttonDown="practiceStart()" />
		<s:Button width="100" label="位置変更" buttonDown="isFlip = !isFlip" />
		<s:Button width="100" label="観戦" buttonDown="leaveBattle()" />
		<s:Button styleName="back" width="100" label="退室" buttonDown="dispatchEvent(new KuzurisEvent(KuzurisEvent.navigateBack))" />
	</s:VGroup>
	<s:HGroup includeIn="watch" x="0" y="450" width="854" verticalAlign="middle" horizontalAlign="center">
		<s:Button styleName="back" width="100" label="退室" buttonDown="dispatchEvent(new KuzurisEvent(KuzurisEvent.navigateBack))" />
	</s:HGroup>
	<v:DetailedRecordView id="recordView1" x="0" y="0" width="118" height="480" battle="true" visible="{isResult}"/>
	<v:DetailedRecordView id="recordView2" x="736" y="0" width="118" height="480" battle="true" visible="{isResult}"/>
	<s:VGroup includeIn="watch" x="143" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button label="参加" buttonDown="enterBattle(0)" />
	</s:VGroup>
	<s:VGroup includeIn="watch" x="561" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button label="参加" buttonDown="enterBattle(1)" />
	</s:VGroup>
</s:Group>
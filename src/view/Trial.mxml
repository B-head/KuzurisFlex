<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/halo" xmlns:v="view.*"
		 enterFrame="onEnterFrame()">

	<fx:Script>
		<![CDATA[
		import event.*;
		import flash.events.*;
		import model.*;
		
		private var mode:String;
		private var manager:GameManager;
		[Bindable]
		private var input:GameControl;
		[Bindable]
		private var record:GameRecord;
		[Bindable]
		private var ranking:GameRanking;
		[Bindable]
		private var pause:Boolean;
		
		public function init(mode:String):void
		{
			this.mode = mode;
			parentApplication.addEventListener(Main.pauseEvent, pauseListener);
			manager = new GameManager(1);
			input = SharedObjectHelper.input;
			manager.addEventListener(GameManagerEvent.gameEnd, endListener);
			main.blockGraphics = new BlockGraphics();
			prepare();
		}
		
		private function prepare():void
		{
			parentApplication.bgm.stop();
			manager.endGame();
			manager.initialize();
			main.gameModel = manager.getGameModel(0);
			currentState = "prepare";
			pause = false;
		}
		
		private function start():void
		{
			var setting:GameSetting = new GameSetting();
			setting.gameMode = mode;
			setting.startLevel = level.value;
			setting.endless = endless.selected;
			setting.levelClearLine = 10;
			manager.initialize();
			manager.setPlayer(0, input);
			main.gameModel = manager.getGameModel(0);
			manager.startGame(setting);
			parentApplication.bgm.play(false);
			currentState = "hidden";
			setFocus();
			pause = false;
		}
		
		private function replayStart():void
		{
			manager.initialize();
			main.gameModel = manager.getGameModel(0);
			manager.startReplay();
			parentApplication.bgm.play(false);
			currentState = "hidden";
			setFocus();
			pause = false;
		}
		
		private function pauseing():void
		{
			parentApplication.bgm.stop();
			manager.phaseGame();
			currentState = "pause";
			pause = true;
		}
		
		private function resume():void
		{
			parentApplication.bgm.play(true);
			manager.resumeGame();
			currentState = "hidden";
			setFocus();
			pause = false;
		}
		
		private function onEnterFrame():void
		{
			if (!manager.isExecution()) return;
			manager.forwardGame();
		}
		
		private function pauseListener(e:Event):void
		{
			if (pause)
			{
				resume();
			}
			else
			{
				if (!manager.isExecution()) return;
				pauseing();
			}
		}
		
		private function endListener(e:GameManagerEvent):void
		{
			parentApplication.bgm.stop();
			manager.endGame();
			record = manager.getRecord(0);
			ranking = SharedObjectHelper.getRanking(mode, endless.selected);
			ranking.entry(record);
			currentState = "end";
			pause = false;
		}
		]]>
	</fx:Script>

	<fx:Metadata>
		[Event(name="back", type="flash.events.Event")]
	</fx:Metadata>

	<s:states>
		<s:State name="hidden" />
		<s:State name="prepare" />
		<s:State name="end" />
		<s:State name="pause" />
		<s:State name="ranking" />
		<s:State name="keyConfig" />
	</s:states>

	<v:GameView id="main" x="227" y="24" battle="false" pause="{pause}"/>
	<s:VGroup includeIn="prepare" x="352" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button styleName="start" width="150" label="スタート" buttonDown="start()"/>
		<s:Label styleName="menu-label" text="開始レベル{level.value}" />
		<s:HSlider id="level" width="150" minimum="1" maximum="20" showDataTip="false" />
		<s:CheckBox id="endless" label="エンドレス" />
		<s:Button width="150" label="ランキング" buttonDown="currentState = 'ranking'" enabled="false" />
		<s:Button width="150" label="キーコンフィグ" buttonDown="currentState = 'keyConfig'"/>
		<s:Button styleName="back" width="100" label="もどる" buttonDown="dispatchEvent(new Event('back'))" />
	</s:VGroup>
	<s:VGroup includeIn="pause" x="352" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button width="150" label="つづける" buttonDown="resume()" />
		<s:Button width="150" label="やりなおす" buttonDown="start()" />
		<s:Button styleName="back" width="100" label="おわる" buttonDown="prepare()" />
	</s:VGroup>
	<s:VGroup includeIn="end" x="352" y="88" width="150" height="320" verticalAlign="middle" horizontalAlign="center">
		<s:Button width="150" label="リプレイ" buttonDown="replayStart()" />
		<s:Button styleName="back" width="100" label="おわる" buttonDown="prepare()" />
	</s:VGroup>
	<v:DetailedRecordView includeIn="end" x="0" y="0" width="223" height="480" battle="false" record="{record}" />
	<v:ResultRankingView includeIn="end" x="631" y="0" width="223" height="480"/>
	<v:KeyConfig includeIn="keyConfig" x="227" y="88" input="{input as UserInput}" back="currentState = 'prepare'"/>
</s:Group>

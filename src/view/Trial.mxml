<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/halo" xmlns:v="view.*"
		 enterFrame="onEnterFrame()">

	<fx:Script>
		<![CDATA[
		import event.GameEvent;
		import model.*;
		import flash.events.*;
		
		private var mode:String;
		private var manager:GameManager;
		private var execution:Boolean;
		[Bindable]
		private var pause:Boolean;
		
		public function init(mode:String):void
		{
			this.mode = mode;
			manager = new GameManager(1);
			manager.setPlayer(0, parentApplication.input);
			manager.addEventListener(GameEvent.gameOver, endListener);
			manager.addEventListener(GameEvent.gameClear, endListener);
			parentApplication.input.addEventListener(UserInput.pauseEvent, pauseListener);
			main.blockGraphics = new BlockGraphics();
			prepare();
		}
		
		private function prepare():void
		{
			parentApplication.bgm.stop();
			currentState = "prepare";
			execution = false;
			pause = false;
		}
		
		private function start():void
		{
			var setting:GameSetting = new GameSetting();
			setting.gameMode = mode;
			setting.startLevel = level.value;
			setting.endless = endless.selected;
			setting.levelClearLine = 10;
			manager.initializeGame(setting);
			main.gameModel = manager.getGameModel(0);
			parentApplication.bgm.play(false);
			currentState = "hidden";
			setFocus();
			execution = true;
			pause = false;
		}
		
		private function pauseing():void
		{
			parentApplication.bgm.stop();
			currentState = "pause";
			execution = false;
			pause = true;
		}
		
		private function resume():void
		{
			parentApplication.bgm.play(true);
			currentState = "hidden";
			setFocus();
			execution = true;
			pause = false;
		}
		
		private function onEnterFrame():void
		{
			parentApplication.input.enable = execution;
			if (!execution)
			{
				return;
			}
			manager.forwardGame();
			main.update();
		}
		
		private function pauseListener(e:Event):void
		{
			if (pause)
			{
				resume();
			}
			else
			{
				if (!execution) return;
				pauseing();
			}
		}
		
		private function endListener(e:GameEvent):void
		{
			parentApplication.bgm.stop();
			currentState = "end";
			execution = false;
			pause = false;
		}
		]]>
	</fx:Script>

	<fx:Metadata>
		[Event(name="back", type="flash.events.Event")]
	</fx:Metadata>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>

	<s:states>
		<s:State name="hidden" />
		<s:State name="prepare" />
		<s:State name="end" />
		<s:State name="pause" />
		<s:State name="ranking" />
		<s:State name="keyConfig" />
	</s:states>

	<v:GameView id="main" x="227" y="24" pause="{pause}"/>
	<s:VGroup includeIn="prepare" x="352" y="224" width="150" horizontalAlign="center">
		<s:Button width="150" label="スタート" buttonDown="start()"/>
		<s:Label text="開始レベル{level.value}" />
		<s:HSlider id="level" width="150" minimum="1" maximum="20" showDataTip="false" />
		<s:CheckBox id="endless" label="エンドレス" />
		<s:Button width="150" label="ランキング" buttonDown="currentState = 'ranking'" enabled="false" />
		<s:Button width="150" label="キーコンフィグ" buttonDown="currentState = 'keyConfig'"/>
		<s:Button width="100" label="もどる" buttonDown="dispatchEvent(new Event('back'))" />
	</s:VGroup>
	<s:VGroup includeIn="pause" x="352" y="224" width="150" horizontalAlign="center">
		<s:Button width="100" label="つづける" buttonDown="resume()" />
		<s:Button width="100" label="やりなおす" buttonDown="start()" />
		<s:Button width="100" label="おわる" buttonDown="prepare()" />
	</s:VGroup>
	<s:VGroup includeIn="end" x="352" y="224" width="150" horizontalAlign="center">
		<s:Button width="100" label="おわる" buttonDown="prepare()" />
	</s:VGroup>
	<v:KeyConfig includeIn="keyConfig"/>
</s:Group>

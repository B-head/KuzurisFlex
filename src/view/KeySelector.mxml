<?xml version="1.0" encoding="utf-8"?>
<s:SkinnableContainer  xmlns:fx="http://ns.adobe.com/mxml/2009" 
               xmlns:s="library://ns.adobe.com/flex/spark" 
               xmlns:mx="library://ns.adobe.com/flex/mx"
			   preinitialize="init()"
			   click="onClick(event)"
			   focusOut="onFocusOut(event)"
			   keyDown="onKeyDown(event)"
			   keyUp="onKeyUp(event)">
	<fx:Script>
		<![CDATA[
			import flash.events.*;
			import flash.ui.Keyboard;
			import flash.utils.describeType;
			import flash.utils.Dictionary;
			
			[Bindable]
			public var labelText:String;
			private var _keys:Vector.<uint>;
			private var keyDictionary:Dictionary;
			private var pressKey:uint;
			
			private function init():void
			{
				keyDictionary = new Dictionary();
				var kd:XML = describeType(Keyboard);
				var kn:XMLList = kd.constant.@name;
				var kl:int = kn.length();
				for (var i:int = 0; i < kl; i++)
				{
					var str:String = String(kn[i]);
					keyDictionary[Keyboard[str]] = str;
				}
				pressKey = uint.MAX_VALUE;
			}
			
			[Bindable(event="keysChanged")]
			public function get keys():Vector.<uint>
			{
				return _keys;
			}
			[Bindable(event="keysChanged")]
			public function set keys(value:Vector.<uint>):void
			{
				_keys = value;
				dispatchEvent(new Event("keysChanged"));
			}
			
			[Bindable(event="keysChanged")]
			private function getKeysText():String
			{
				var ret:String = "";
				for (var i:int = 0; i < keys.length; i++)
				{
					if (i > 0)
					{
						ret += ", ";
					}
					ret += keyDictionary[keys[i]];
				}
				return ret;
			}
			
			private function onClick(e:MouseEvent):void
			{
				setFocus();
				if (currentState == "normal")
				{
					currentState = "selected";
				}
				else if (currentState == "selected")
				{
					currentState = "normal";
				}
			}
			
			private function onFocusOut(e:FocusEvent):void
			{
				currentState = "normal";
				pressKey = uint.MAX_VALUE;
			}
			
			private function onKeyDown(e:KeyboardEvent):void
			{
				if (currentState != "selected") return;
				pressKey = e.keyCode;
			}
			
			private function onKeyUp(e:KeyboardEvent):void
			{
				if (currentState != "selected") return;
				if (pressKey != e.keyCode) return;
				currentState = "normal";
				if (parentApplication.pause.indexOf(e.keyCode) != -1) return;
				var i:int = _keys.indexOf(e.keyCode);
				if (i == -1)
				{
					_keys.push(e.keyCode);
				}
				else
				{
					_keys.splice(i, 1);
				}
				dispatchEvent(new Event("keysChanged"));
			}
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="normal" />
		<s:State name="selected" />
	</s:states>
	
	<s:Label x="0" y="0" text="{labelText}"/>
	<s:Label x="100" y="0" text="{getKeysText()}"/>
</s:SkinnableContainer >